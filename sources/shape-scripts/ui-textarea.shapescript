shape main{

	noshadow="true";
	layoutType="leftright";

	defSize(100,50);
		
	addsubshape("input", 100, 100);
		
	shape input {
		layoutType="leftright";

		setfillcolor(255,255,255);
		rectangle(0,0,100,100);			
	
		addsubshape("padding", 2, 100);
		addsubshape("fieldName",96, 100);

	}

	shape padding {
		print("");
	}

	shape fieldInfo {
		if (HasTag("Field Mode", "Visible")) {
			if (HasTag("Field")) {
				print("#tag:Field#");
			} else {
				print("dataName");
			}
		}
	}

	shape fieldName {	
		layoutType="topdown";

		addsubshape("padding", 100, 12);
		addsubshape("fieldInfo",100, 88);		
	}
	
}

shape label {		
	setOrigin("W", -6, 0);	

	if (HasTag("Label Mode", "Visible")) {

		if (HasTag("Label Type")) {

			// Statix text label
			if (HasTag("Label Type", "Static Text")) {
				if (HasTag("Label")) {
					print("#tag:Label#");
				} else {
					print("#NAME#");
				}			
			} 

			// Label defined by a UI variable or data field
			if (HasTag("Label Type", "UI Field")) {
				if (HasTag("Label")) {
					print("[#tag:Label#]");
				} else {
					print("[#NAME#]");
				}			
			} 
		} else {
			if (HasTag("Label")) {
				print("#tag:Label#");
			} else {
				print("#NAME#");
			}						
		}		

		
		// The label is suffixed with a colon when the `Label Suffix` feature is set to `Visible` or when this feature is not defined
		if (HasTag("Label Suffix")) {
			if (HasTag("Label Suffix", "Visible")) {
				print(": ");
			}
		}
		else {
			print(": ");			
		}	

	}	
}


decoration sizer {
	orientation="SE";
	
	moveto(80,100);
	lineto(100,80);
	moveto(60,100);
	lineto(100,60);
	
}
