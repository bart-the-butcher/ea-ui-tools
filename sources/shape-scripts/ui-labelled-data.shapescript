shape main{	
	noshadow="true";
	layoutType="leftright";
	
	defSize(100,18);

	addsubshape("padding", 2, 100);
	addsubshape("fieldName",96, 100);

	shape padding {
		print("");
	}

	shape fieldInfo {
		if (HasTag("Field Mode", "Visible")) {
			if (HasTag("Field")) {
				print("[#tag:Field#]");
			} else {
				print("[#NAME#]");
			}
		} else {
			if (HasTag("Text")) {
				print("#tag:Text#");
			} else {
				print("#NAME#");
			}			
		}
	}
	
	shape fieldName {	
		layoutType="topdown";
		
		addsubshape("padding", 100, 12);
		addsubshape("fieldInfo",100, 88);		
	}		
}

shape label {		

	setOrigin("W", -6, 0);	

	if (HasTag("Label Type")) {
		
		// Statix text label
		if (HasTag("Label Type", "Static Text")) {
			if (HasTag("Label")) {
				print("#tag:Label#");
			} else {
				print("#NAME#");
			}			
		} 
		
		// Label defined by a UI variable or data field
		if (HasTag("Label Type", "UI Field")) {
			if (HasTag("Label")) {
				print("[#tag:Label#]");
			} else {
				print("[#NAME#]");
			}			
		} 
		
		// The label is suffixed with a colon when the `Label Suffix` feature is set to `Visible` or when this feature is not defined
		if (HasTag("Label Suffix")) {
			if (HasTag("Label Suffix", "Visible")) {
				print(": ");
			}
		}
		else {
			print(": ");			
		}	
	}
}

