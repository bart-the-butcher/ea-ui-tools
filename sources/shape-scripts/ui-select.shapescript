shape main{
	noshadow="true";
	layoutType="leftright";

	defSize(100,18);
		
	addsubshape("input", 100,100);

	shape input {
		layoutType="leftright";

		setfillcolor(255,255,255);
		rectangle(0,0,100,100);			
	
		addsubshape("padding", 2, 100);
		addsubshape("fieldName",96, 100);

	}

	shape padding {
		print("");
	}

	shape fieldInfo {
		if (HasTag("Field Mode", "Visible")) {
			if (HasTag("Field")) {
				print("#tag:Field#");
			} else {
				print("dataName");
			}
		}
	}

	shape fieldName {	
		layoutType="topdown";

		addsubshape("padding", 100, 6);
		addsubshape("fieldInfo",100, 90);		
	}

}


shape label {		
	setOrigin("W", -6, 0);	

	if (HasTag("Label Mode", "Visible")) {

		if (HasTag("Label Type")) {

			// Statix text label
			if (HasTag("Label Type", "Static Text")) {
				if (HasTag("Label")) {
					print("#tag:Label#");
				} else {
					print("#NAME#");
				}			
			} 

			// Label defined by a UI variable or data field
			if (HasTag("Label Type", "UI Field")) {
				if (HasTag("Label")) {
					print("[#tag:Label#]");
				} else {
					print("[#NAME#]");
				}			
			} 
		}
		
		// The label is suffixed with a colon when the `Label Suffix` feature is set to `Visible` or when this feature is not defined
		if (HasTag("Label Suffix")) {
			if (HasTag("Label Suffix", "Visible")) {
				print(": ");
			}
		}
		else {
			print(": ");			
		}	

	}	
}

decoration selector {
	orientation="E";
	
	// separate line
	moveto(0,0);
	lineto(0,100);

	startpath();
		moveto(20,26);
		lineto(100,26);
		lineto(60,74);
		lineto(20,26);
	endpath();
    setfillcolor(0,0,255);
    fillandstrokepath();	
}